
plugins {
	id 'com.cookpad.android.plugin.license-tools' version '1.2.5'
	id 'java'
	id 'maven-publish'
	id 'application'
	id 'distribution'
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	// implementation 'org.slf4j:slf4j-api: 1.7.36'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
}

group = 'com.beyondrelations.filesystem'
version = 1.0
description = 'Launch the latest beycube jar, assuming Z:|data|bey|releases|(lots of folders)'

compileJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

sourceCompatibility = '1.8'

licenseTools {
	outputJson = "cookpad-license-report-flat.json"
}

java {
	withSourcesJar()
}

jar {
	manifest {
		attributes(
			'Main-Class' : 'com.beyondrelations.filesystem.BeyJarLauncher',
			'Automatic-Module-Name' : 'com.beyondrelations.filesystem',
			'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join( ' ' )
		)
	}
	from "LICENSE"
}

task uberJar(type: Jar) {
	archiveClassifier = 'uber'

	from sourceSets.main.output

	dependsOn configurations.runtimeClasspath
	from {
		configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree( it ) }
	}
}

task run_( type : JavaExec ) {
	dependsOn jar
	classpath files( jar.archivePath )
	main = 'com.beyondrelations.filesystem.BeyJarLauncher'
}

run {
	mainClassName = 'com.beyondrelations.filesystem.BeyJarLauncher'
}


publishing {
	publications {
		maven( MavenPublication ) {
			from( components.java )
			pom {
				name = project.name
				inceptionYear = '2022'
				url = 'https://github.com/BR-Nicholas-Prado/Beycube_jarlauncher'
				packaging = 'jar'
				licenses {
					license {
						name = 'Mozilla public v2.0'
						url = 'https://www.mozilla.org/en-US/MPL/2.0'
					}
				}
				developers {
					developer {
						id = 'Br-Nicholas-Prado'
						name = 'Nicholas Prado'
						email = 'nick@getworkflo'
					}
				}
				scm {
					connection = ''
					developerConnection = 'git@github.com:BR-Nicholas-Prado/BR_Splaintime.git'
					url = 'https://github.com/BR-Nicholas-Prado/BR_Splaintime'
				}
			}
			artifact distZip
		}
	}
}

distributions {
	main {
		distributionBaseName = 'BeyJarLauncher'
		contents {
			from 'LICENSE',
					'readme.adoc',
					'src/main/assets/cookpad-license-report-flat.json',
					'changelog.adoc'
		}
	}
}


































